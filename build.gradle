// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
    project.ext {
        buildToolsVersion = "26.0.0-rc2"
        compileSdkVersion = "android-O"
        targetSdkVersion = 23
        minSdkVersion = 16

        // Common depndencies
        def supportLibVersion = '26.0.0-beta1'

        design = 'com.android.support:design:' + supportLibVersion
        palette = 'com.android.support:palette-v7:' + supportLibVersion
        cardView = 'com.android.support:cardview-v7:' + supportLibVersion
        supportV4 = 'com.android.support:support-v4:' + supportLibVersion
        appCompat = 'com.android.support:appcompat-v7:' + supportLibVersion
        recyclerView = 'com.android.support:recyclerview-v7:' + supportLibVersion
        supportAnnotations = 'com.android.support:support-annotations:' + supportLibVersion

        // Artifactory publishing params
        Properties libProps = new Properties()
        Properties bintrayProps = new Properties()

        libProps.load(new FileInputStream(file("libraryVersion.properties")))
        bintrayProps.load(new FileInputStream(file("bintray.properties")))

        groupId = libProps['groupId']
        libraryVersion = libProps['libraryVersion']
        artifactoryUrl = libProps['artifactoryUrl']

        bintrayUser = bintrayProps['user']
        bintrayApiKey = bintrayProps['apiKey']
    }
}

// Plugin used to upload authenticated files to BinTray through Gradle
plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
