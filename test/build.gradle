apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion parent.ext.compileSdkVersion
    buildToolsVersion parent.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion parent.ext.minSdkVersion
        targetSdkVersion parent.ext.targetSdkVersion

        consumerProguardFiles 'proguard-rules.pro'

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    libraryVariants.all { variant ->
//        variant.outputs.each { output ->
//            def outputFile = output.outputFile
//            if (outputFile != null && outputFile.name.endsWith('.aar')) {
//                def fileName = "${project.getName()}-${parent.ext.libraryVersion}.aar"
//                output.outputFile = new File(outputFile.parent, fileName)
//            }
//        }
        variant.outputs.all {
            outputFileName = "${project.getName()}-${parent.ext.libraryVersion}.aar"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile parent.ext.appCompat
    compile parent.ext.recyclerView

    compile 'junit:junit:4.12'
    compile 'org.hamcrest:hamcrest-library:1.3'
    compile 'org.mockito:mockito-android:2.7.22'
    compile 'com.android.support.test:runner:0.5'

    compile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support'
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
    compile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support'
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
    compile('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
}

bintray {
    user = parent.ext.bintrayUser
    key = parent.ext.bintrayApiKey
    // jFrog plugin must be declared for this line to work
    configurations = ['archives']
    publications = ['lib']
    // Package info for BinTray
    pkg {
        repo = 'maven'
        name = 'android-bootstrap'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/tunjid/android-bootstrap.git'
        version {
            name = '0.1'
            desc = 'An Android library with components for Android unit, integration and UI tests.'
            vcsTag = '0.1'
        }
    }
}

publishing {
    publications {
        lib(MavenPublication) {
            version = parent.ext.libraryVersion
            groupId parent.ext.groupId
            artifactId project.getName()

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-${version}.aar")

            // Add sources too
            artifact(sourcesJar) {
                classifier = 'sources'
                extension = 'jar'
            }

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if (it.group != null && it.name != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url parent.ext.artifactoryUrl
        }
    }
}

// Gradle task to generate sources after building a release aar
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn 'sourcesJar'
    }
}